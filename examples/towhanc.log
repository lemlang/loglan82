BLOCK
(* TOWERS OF HANOI *)
 
(* THERE ARE THREE TOWERS BUILT OF DECREASING RINGS STRINGED ONTO STICKS    *)
(* AT THE INITIAL STATE ALL RINGS ARE STRINGED ONTO STICK NO. 1. OUR JOB IS *)
(* TO MOVE ALL RINGS FROM THE STICK 1 TO THE STICK 3. THE DIFFICULTY IS     *)
(* THAT WE MUSTN'T VIOLATE THE FOLLOWING CONDITIONS                         *)
(*    1. WE CAN MOVE ONLY ONE RING AT ONE STEP                              *)
(*    2. EACH RING MAY BE PLACED ONLY ONTO A GREATER ONE                    *)
(* TO MANAGE WITH THIS DIFFICULT PROBLEM WE HAVE AN AUXILLIARY STICK 2      *)
 
 UNIT WZ:COROUTINE(N,F,T:INTEGER);
   (* MOVE N RINGS FROM STICK F TO STICK T *)
      VAR K:INTEGER;
  BEGIN
   RETURN;
   DO
   K:=6-(F+T);
   IF N>1 THEN  ATTACH (P(N-1,F,K)); FI;
   CALL MODYF(F,T);  (* MOVE ONLY ONE RING *)
   IF N>1 THEN ATTACH (P(N-1,K,T)); FI;
    DETACH;
    OD;
   END WZ;
 
UNIT MODYF:PROCEDURE(F,T:INTEGER);
   (* MOVE THE TOPMOST RING FROM STICK F TO STICK T *)
 BEGIN
   TOP(T):=TOP(T)+1;
   W(T,TOP(T)):=W(F,TOP(F));
   W(F,TOP(F)):=0;
   TOP(F):=TOP(F)-1;
   CALL DISPL;
 END MODYF;
 
UNIT DISPL:PROCEDURE;
  (* PRINTING *)
 VAR T,I,J,K,M,N:INTEGER;
  BEGIN
   T:=1;
   FOR I:=2 TO 3 DO
    IF TOP(I)>TOP(T) THEN T:=I FI OD;
   T:=TOP(T);
   FOR I:=T DOWNTO 1 DO
    M:=15;
    FOR J:=1 TO 3 DO
     FOR K:=1 TO M DO WRITE(" "); OD;
     IF W(J,I)=/=0 THEN FOR K:=1 TO W(J,I) DO WRITE("*") OD;
     FI;
     M:=15-W(J,I);
    OD;
    WRITELN;
  OD;
  FOR I:=1 TO 15 DO WRITE(" "); OD;
  FOR I:=1 TO 45 DO WRITE("-"); OD;
  WRITELN;
 END DISPL;
 
 VAR  W:ARRAYOF ARRAYOF INTEGER,   (* HOW MANY RINGS ARE STRINGED *)
                                   (* ON EACH STICK               *)
      TOP:ARRAYOF INTEGER,  (* THE TOPMOST RING SIZE ON EACH STICK *)
      NB,I,J,K,TIMEB:INTEGER,
      P:ARRAYOF ARRAYOF ARRAYOF WZ; (* COROUTINE POINTERS *)
 
   BEGIN
    ARRAY W DIM(1:3);
    ARRAY TOP DIM(1:3);
    WRITELN(" PROGRAM TOWERS OF HANOI");
    WRITELN(" VERSION WITH COROUTINES");
    DO WRITELN(" GIVE THE NUMBER OF RINGS");
       READ(NB);
       WRITELN(NB);
       IF NB>0 THEN EXIT ELSE WRITELN(" NUMBER OF RINGS MUST BE GREATER THAN 0")
    FI OD;
    TIMEB:=TIME;
    TOP(1):=NB;
    ARRAY W(1) DIM(1:NB);
    ARRAY W(2) DIM(1:NB);
    ARRAY W(3) DIM(1:NB);
    K:=NB;
    FOR I:=1 TO NB DO W(1,I):=K;
                      K:=K-1;
    OD;
    (* STICK 1 IS FULL *)
    WRITELN(" THE ALGORITHM ACTS AS FOLLOWS");
    CALL DISPL;
    ARRAY P DIM (1:NB);
    FOR I:=1 TO NB
    DO ARRAY P(I) DIM(1:3);
      FOR J:=1 TO 3
      DO ARRAY P(I,J) DIM(1:3);
         FOR K:=1 TO 3
         DO IF J=/=K THEN P(I,J,K):=NEW WZ(I,J,K) FI
         OD
      OD
    OD;
    ATTACH (P(NB,1,3));
    WRITELN(" EXECUTION TIME FOR",NB:4," RINGS =",TIME-TIMEB," SEC");
END 
